#!/usr/bin/env coffee

base_path = process.cwd()

require('app-module-path').addPath("#{base_path}/node_modules")
program = require('commander')
fs = require('graceful-fs')
path = require('path')
chokidar = require('chokidar')
transformer = require('jstransformer')
recursive = require('fs-readdir-recursive')
matter = require('gray-matter')
b_sync = require('browser-sync')
hljs = undefined

tfunk = require("tfunk")

log = () ->

  args = Array::slice.call(arguments)

  log_types = ['log', 'warn', 'error', 'debug']

  if args[0] and args[1] and args[2]
    type = args[0]
    sender = args[1]
    strings = args[2..]

  else if args[0] and args[1]
    strings = args[1..]

    if log_types.includes args[0]
      type = args[0]

    else
      sender = args[0]

  else
    strings = args[0..]

  type ||= 'log'
  sender ||= 'glug'

  string = strings.join(' ')

  switch sender
    when 'glug'
      color = 'green'
    else
      color = 'yellow'

  prefix = "[{#{color}:#{sender}}]"

  switch type
    when 'log'
      color = 'gray'
    when 'warn'
      color = 'yellow'
    when 'error'
      color = 'red'
    when 'debug'
      color = 'blue'
    else
      color = 'gray'

  prefix += "[{#{color}:#{type}}] "

  return tfunk(prefix + string)

program
  .option('-v, --verbose', 'print more output')
  .parse(process.argv)

print = console.log

info = () ->

debug = () ->
  if program.verbose?
    print log 'debug', arguments...

config_path = "#{base_path}/config"

try
  config = require(config_path)
catch error
  throw log 'error', "config file not found at #{config_path}"
  
bs = b_sync.create()

merge = ->
  new_object = {}
  for object in arguments
    for key of object
      new_object[key] = object[key]
  new_object

write_file = (path, contents) ->
  debug "about to write #{path}"

  fs.writeFile path, contents, (err) ->
    if err
      throw err

rm_dir = (path) ->
  if fs.existsSync(path)
    fs.readdirSync(path).forEach (file, index) ->
      curPath = path + '/' + file
      if fs.lstatSync(curPath).isDirectory()
        # recurse
        deleteFolderRecursive curPath
      else
        # delete file
        fs.unlinkSync curPath
      return
    fs.rmdirSync path

transformers = {}

transformers = merge(transformers, config.transformers)

renderers = {}

debug 'about to require jstransformers'
for name, options of transformers
  debug "Requiring JSTransformer:" + name

  try
    renderers[name] = transformer(require("jstransformer-#{name}"))

  catch error
    throw "#{error}\n\nTry running `npm install --save jstransformer-#{name}`"

debug 'done requiring jstransformers'

paths = {}

input_dir  = config.input_dir  || 'app'
output_dir = config.output_dir || 'public'

if config.transformers['markdown-it']? and config.transformers['markdown-it'].highlight?
  hljs = require('highlight.js')
  transformers['markdown-it'].highlight = (str, lang) ->
    if lang and hljs.getLanguage(lang)
      try
        return hljs.highlight(lang, str).value
      catch __
    ''

transformers_for = (filetype) ->
  renderer_names = []
  for name, transformer of transformers
    renderer = renderers[name]
    if renderer.inputFormats.includes(filetype)
      renderer_names.push name

  if renderer_names.length is 0
    # print 'renderer_names is empty'
    return []

  last_renderer_name = renderer_names[renderer_names.length - 1]
  last_extension = renderers[last_renderer_name].outputFormat
  if last_extension isnt filetype
    for new_renderer in transformers_for(last_extension)
      renderer_names.push new_renderer

    # print "#{filetype} => #{last_extension}"
  return renderer_names

all_files = recursive(input_dir)

all_files = all_files.map (file) ->
  path.join(input_dir, file)

files = []

for file in all_files
  if path.basename(file)[0] isnt '_'
    files.push(file)

debug 'making file list'

bs_options = merge({
  server: output_dir
}, config.server)

bs.init(bs_options)

rm_dir output_dir

unless fs.existsSync(output_dir)
  fs.mkdir(output_dir)

log 'info', 'rendering files'

render = (contents, input_format, settings={}, callback) ->
    file_data = matter(contents)

    contents = file_data.content
    frontmatter = file_data.data

    renderer_names = transformers_for input_format

    default_transformers_config =
      stylus:
        paths: [
          input_dir
        ]

    rendered_contents = contents

    for renderer_name in renderer_names
      renderer = renderers[renderer_name]
      renderer_config = transformers[renderer_name]

      output_format = renderer.outputFormat

      renderer_config = merge(settings, renderer_config, frontmatter)

      rendered_contents = renderer.render(rendered_contents, renderer_config).body

    if output_format is 'html'
      frontmatter.layout ||= config.default_layout || 'layout'

    if frontmatter.layout? and not renderer_config.is_layout
      debug "layout is #{frontmatter.layout}"
      layout_location = "#{input_dir}/#{config.views_dir}/#{frontmatter.layout}.#{config.layout_extension}"
      layout_content = fs.readFileSync layout_location, { encoding: 'utf8' }

      layout_config = merge(renderer_config, 
        contents: rendered_contents
        is_layout: true
      )

      new_contents = render(layout_content,
        config.layout_extension,
        layout_config,
        callback)

    else
      callback(rendered_contents)

render_file = (file) ->
  extension = path.extname(file).replace('.', '')

  renderer_names = transformers_for extension

  renderer_name = renderer_names[0]

  renderer = renderers[renderer_name]

  new_extension = renderer.outputFormat
  basename = path.basename(file, extension)
  new_name = basename + new_extension
  new_path = path.join(output_dir, new_name)

  input_format = extension
  last_renderer = renderer_names[renderer_names.length - 1]
  output_format = renderers[last_renderer].outputFormat

  list_of_renderers = renderer_names.join(' => ')
  debug ".#{input_format} => #{list_of_renderers} => .#{output_format}"


  fs.readFile file, { encoding: 'utf8' }, (err, text) ->

    if err
      throw err

    render(text, extension, {filename: file}, (rendered_contents) ->
      write_file(new_path, rendered_contents)
    )

  return new_path

chokidar.watch(input_dir, {}).on 'all', (event, file) ->
  debug "#{event} #{file}"
  if event is 'change' or event is 'remove' or event is 'add'
    files_to_reload = [file]

    if path.basename(file)[0] is '_'
      file_type = path.extname(file)
      debug "#{file} is a #{file_type} partial"
      for other_file in files
        if path.extname(other_file) is file_type
          files_to_reload.push other_file

    if file.includes('layout')
      for other_file in files
        files_to_reload.push other_file

    for file in files_to_reload
      output_file = render_file(file).replace(output_dir + '/', '')
      bs.reload(output_file)

debug 'script ended'
